/*
 * ART-2a Clustering for Java
 * Copyright (C) 2025 Jonas Schaub, Betuel Sevindik, Achim Zielesny
 *
 * Source code is available at
 * <https://github.com/JonasSchaub/ART2a-Clustering-for-Java>
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

plugins {
    id 'org.gradle.java'
    id 'jacoco'
    id 'java-library'
    id 'java'
    id("com.diffplug.spotless") version "8.0.0"
    id "com.vanniktech.maven.publish" version "0.35.0"
    id "org.sonarqube" version "7.0.1.6134"
}

group = 'io.github.jonasschaub'
//see also version for publishing below! And do not forget to update CITATION.cff version as well!!!
version = '2.0.0'

//sourceCompatibility = 1.17

//Creates javadoc and sources jars
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.jetbrains', name : 'annotations', version: '23.0.0'
    testImplementation("org.junit.jupiter:junit-jupiter:6.0.1")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    forkEvery = 1

    testLogging {
        showStandardStreams = true
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn test // tests are required to run before generating the report
}

spotless {
    java {
        encoding 'UTF-8'
        cleanthat()
        importOrder('de', 'org', 'com', 'java', 'javax')
        removeUnusedImports()
        indentWithSpaces(4) // doesn't really seem to work...
        trimTrailingWhitespace()
        endWithNewline()
        licenseHeaderFile('License-header/License-header.txt')
        //eclipse() //not optimal, because indents with tabs..
        //googleJavaFormat() //not optimal, because indents with two spaces...
        //palantirJavaFormat() //not optimal, because unnecessary line breaks in head of for loop and corrupts editor folds
        //prettier() //needs npm installed, unsuitable...
        //clangFormat() //also needs an installation...
        custom 'Refuse wildcard imports', {
            // Wildcard imports can't be resolved by spotless itself.
            // This will require the developer themselves to adhere to best practices.
            if (it =~ /\nimport .*\*;/) {
                throw new AssertionError("Do not use wildcard imports. 'spotlessApply' cannot resolve this issue.")
            }
        }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "JonasSchaub_ART2a-Clustering-for-Java"
        property "sonar.organization", "jonasschaub"
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

mavenPublishing {
    publishToMavenCentral(true, false) // automaticRelease = true, validateDeployment = false

    signAllPublications()

    coordinates("io.github.jonasschaub", "ART2a-Clustering-for-Java", "2.0.0")

    pom {
        name = "ART2a-Clustering-for-Java"
        description = "Implementation of the ART 2-A clustering algorithm in Java."
        url = "https://github.com/JonasSchaub/ART2a-Clustering-for-Java"
        inceptionYear = "2023"
        licenses {
            license {
                name = "MIT"
                url = "https://opensource.org/license/mit/"
                distribution = "https://opensource.org/license/mit/"
            }
        }
        developers {
            developers {
                developer {
                    id = "B-s123"
                    name = "Betuel Sevindik"
                    email = "betuel.sevindik@studmail.w-hs.de"
                    url = "https://github.com/B-s123"
                }
                developer {
                    id = "JonasSchaub"
                    name = "Jonas Schaub"
                    email = "jonas.schaub@uni-jena.de"
                    url = "https://github.com/JonasSchaub"
                }
                developer {
                    id = "zielesny"
                    name = "Achim Zielesny"
                    email = "achim.zielesny@w-hs.de"
                    url = "https://www.w-hs.de/service/informationen-zur-person/person/zielesny/"
                }
            }
        }
        scm {
            connection = 'scm:git:git://github.com/JonasSchaub/ART2a-Clustering-for-Java.git'
            developerConnection = 'scm:git:ssh://github.com/JonasSchaub/ART2a-Clustering-for-Java.git'
            url = 'https://github.com/JonasSchaub/ART2a-Clustering-for-Java/'
        }
    }

    tasks.named('generateMetadataFileForMavenPublication') {
        dependsOn tasks.plainJavadocJar
    }
}
